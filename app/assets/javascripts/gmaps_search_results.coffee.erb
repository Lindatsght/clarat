initialize = ->
  canvas = document.getElementById('map-canvas')
  markers = $(canvas).data('markers')
  includedPoints = []

  if markers
    # Create Map
    mapOptions =
      scrollwheel: false
    map = new google.maps.Map(canvas, mapOptions)
    bounds = new google.maps.LatLngBounds()

    # Create Markers
    for key, markerData of markers
      continue unless markerData.position
      markerPosition = new google.maps.LatLng(
        markerData.position.latitude,
        markerData.position.longitude
      )
      marker = new google.maps.Marker
        position: markerPosition
        map: map
        icon: "<%= asset_path('gmaps_marker_1.svg') %>"

      includedPoints.push markerPosition
      bounds.extend markerPosition

      # Bind Event Listeners To Marker
      bindMapsEvents map, marker, markerData
      bindMarkerToResults marker, markerData

    # Get User Position
    userPosition = $(canvas).data('position')
    if userPosition
      # Include User Position in Map
      userPosition = new google.maps.LatLng(
        userPosition.latitude,
        userPosition.longitude
      )
      includedPoints.push userPosition
      bounds.extend userPosition

    # Expand Map to Include All Markers
    if includedPoints.length > 1
      map.fitBounds bounds
    else
      map.setCenter bounds.getCenter()
      map.setZoom 15

    bindExternalEvents()


bindMapsEvents = (map, marker, markerData) ->

  contentString = '<div id="content">' + markerData.offer_ids[0] + '</div>'
  infowindow = new (google.maps.InfoWindow)(content: contentString)

  console.log markerData
#  console.log marker

  google.maps.event.addListener marker, 'click', (event) ->
    offerID = markerData.offer_ids[0] # specification needed

    map.setCenter marker.getPosition()
    infowindow.open map, marker

bindMarkerToResults = (marker, markerData) ->
  for offerID in markerData.offer_ids
    $("#result-offer-#{offerID}").data('marker', marker)

bindExternalEvents = ->
  $('.JS-trigger-marker').on 'mouseover', (event) ->
    marker = $(event.delegateTarget).data('marker')
    if marker and marker.getAnimation() == null
      marker.setAnimation google.maps.Animation.BOUNCE

  $('.JS-trigger-marker').on 'mouseout', (event) ->
    marker = $(event.delegateTarget).data('marker')
    if marker and marker.getAnimation() != null
      marker.setAnimation null

google.maps.event.addDomListener window, 'load', initialize
google.maps.event.addDomListener document, 'page:load', initialize
